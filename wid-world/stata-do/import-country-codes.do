// -------------------------------------------------------------------------- //
// Export countries and region codes.
//
// ** Must re-run if the following raw file/s have changed **
// 		$country_codes/country-codes-new.xlsx
//
// Requires:
//		$country_codes/country-codes-new.xlsx
// Produces:
//		import-country-codes-output.dta
//		import-region-codes-output.dta
//		import-region-codes-mer-output.dta
// -------------------------------------------------------------------------- //

// Check if raw file/s have changed before running Do-file
local cou_exists = fileexists("$work_data/import-country-codes-output.dta")
local ppp_exists = fileexists("$work_data/import-region-codes-output.dta")
local mer_exists = fileexists("$work_data/import-region-codes-mer-output.dta")
if `cou_exists' & `ppp_exists' & `mer_exists' {
	quietly ashell date -r $country_codes/country-codes-new.xlsx +%s
	local codes_modified = r(o1)
	quietly ashell date -r $work_data/import-country-codes-output.dta +%s
	local last_update = r(o1)
	if `last_update' > `codes_modified' {
		di "No country codes update needed!"
		exit
	}
	else {
		di "Converting country codes."
	}
}

// Run normal code
import excel "$country_codes/country-codes-new.xlsx", clear firstrow

keep if shortname != ""
rename code iso

// -------------------------------------------------------------------------- //
// Countries only
// -------------------------------------------------------------------------- //

preserve
	drop if iso == "WO" | ///
		inrange(iso, "QB", "QZ") | ///
		inrange(iso, "OA", "OJ") | ///
		inrange(iso, "XA", "XF") | ///
		inrange(iso, "XL", "XS")
	label data "Generated by import-country-codes.do"
	save "$work_data/import-country-codes-output.dta", replace
restore

// -------------------------------------------------------------------------- //
// Regions (PPP) and Regions (MER)
// -------------------------------------------------------------------------- //

keep if iso == "WO" | ///
	inrange(iso, "QB", "QZ") | ///
	inrange(iso, "OA", "OJ") | ///
	inrange(iso, "XA", "XF") | ///
	inrange(iso, "XL", "XS")
keep iso titlename shortname

// replace titlename = titlename + " (at purchasing power parity)"
// replace shortname = shortname + " (at purchasing power parity)"

label data "Generated by import-country-codes.do"
save "$work_data/import-region-codes-output.dta", replace

replace iso = iso + "-MER"
replace titlename = titlename + " (at market exchange rate)"
replace shortname = shortname + " (at market exchange rate)"

label data "Generated by import-country-codes.do"
save "$work_data/import-region-codes-mer-output.dta", replace

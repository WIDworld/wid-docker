use "$wid_dir/Country-Updates/China/2017/PYZ2017_WID_export.dta", clear

rename alpha2 iso
rename perc p
destring year, replace
replace p = "pall" if p == "p0p100"

rename wwealn wwealn999i
rename wwealp wwealp999i
rename wwealc wwealc999i
rename wwealg wwealg999i

rename mnwdom999i mnwodm999i
rename anwdom999i anwodm999i
rename anwdom992i anwodm992i

replace inyixx999i = 1/inyixx999i

foreach v of varlist aptinc992j-anwgxd992i {
	rename `v' value`v'
}
reshape long value, i(iso p year) j(widcode) string
drop if missing(value)

generate currency = "CNY" if inlist(substr(widcode, 1, 1), "a", "t", "m", "i", "o", "x")

tempfile china
save "`china'", replace

// Create metadata
generate sixlet = substr(widcode, 1, 6)
keep iso sixlet
duplicates drop
generate source = `"[URL][URL_LINK]http://wid.world/document/t-piketty-l-yang-and-g-zucman-capital-accumulation-private-property-and-inequality-in-china-1978-2015-2016/[/URL_LINK][URL_TEXT]"' ///
	+ `"Piketty, Thomas; Yang, Li and Zucman, Gabriel (2016). "' ///
	+ `"Capital Accumulation, Private Property and Rising Inequality in China, 1978-2015[/URL_TEXT][/URL]; "'
generate method = ""
tempfile meta
save "`meta'"

use "$work_data/add-us-data-output.dta", clear

drop if iso == "CN"
append using "`china'"

label data "Generated by add-china-data.do"
save "$work_data/add-china-data-output.dta", replace

// Change metadata for China

use "$work_data/add-us-data-metadata.dta", clear
merge 1:1 iso sixlet using "`meta'", nogenerate update replace

label data "Generated by add-china-data.do"
save "$work_data/add-china-data-metadata.dta", replace

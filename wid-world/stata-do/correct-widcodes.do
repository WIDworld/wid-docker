// -------------------------------------------------------------------------- //
// Make some corrections because some widcodes for national wealth had to be
// changed: to be eventually integrated to the above files
// add-researchers-data-output.dta
//		-> correct-widcodes-output.dta
// add-researchers-data-metadata.dta
//		-> correct-widcodes-metadata.dta
// -------------------------------------------------------------------------- //

assert fileexists("$work_data/add-researchers-data-metadata.dta")
local data_exists = fileexists("$work_data/correct-widcodes-output.dta")
local meta_exists = fileexists("$work_data/correct-widcodes-metadata.dta")
if (`data_exists' & `meta_exists') {
	quietly ashell date -r $work_data/correct-widcodes-metadata.dta +%s
	local converted = r(o1)
	quietly ashell date -r $work_data/add-researchers-data-metadata.dta +%s
	local data_changed = r(o1)
	if `converted' > `data_changed' {
		di "No action needed!"
		exit
	}
}

use "$work_data/add-researchers-data-output.dta", clear

local old_codes nwhom nwodm nwnfm nwbum nwdem nwfim
local new_codes nwhou nwodk nwnfa nwbus nwdeb nwfin
generate changed = 0
forvalues i = 1/6 {
	local old: word `i' of `old_codes'
	local new: word `i' of `new_codes'
	
	replace changed = 1 if substr(widcode, 2, 5) == "`old'"
	replace widcode = substr(widcode, 1, 1) + "`new'" + substr(widcode, 7, 4) if substr(widcode, 2, 5) == "`old'"
}
duplicates tag iso year p widcode, generate(dup)
// We remove the duplicate that hasn't changed because the last updates
// of the data follow the old codes, while the old WTID data have been
// automatically updated to the new codes via WID_dictionary.xlsx
drop if dup & !changed
drop dup changed
duplicates tag iso year p widcode, generate(dup)
assert dup == 0
drop dup
label data "Generated by correct-widcodes.do"
save "$work_data/correct-widcodes-output.dta", replace

use "$work_data/add-researchers-data-metadata.dta", clear

local old_codes nwhom nwodm nwnfm nwbum nwdem nwfim
local new_codes nwhou nwodk nwnfa nwbus nwdeb nwfin
generate changed = 0
forvalues i = 1/6 {
	local old: word `i' of `old_codes'
	local new: word `i' of `new_codes'
	
	replace changed = 1 if substr(sixlet, 2, 5) == "`old'"
	replace sixlet = substr(sixlet, 1, 1) + "`new'" if substr(sixlet, 2, 5) == "`old'"
}
duplicates tag iso sixlet, generate(dup)
// We remove the duplicate that hasn't changed because the last updates
// of the data follow the old codes, while the old WTID data have been
// automatically updated to the new codes via WID_dictionary.xlsx
drop if dup & !changed
drop dup changed
duplicates tag iso sixlet, generate(dup)
assert dup == 0
drop dup
label data "Generated by correct-widcodes.do"
save "$work_data/correct-widcodes-metadata.dta", replace

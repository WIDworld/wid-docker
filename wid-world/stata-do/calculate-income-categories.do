use "$work_data/add-macro-data-output.dta", clear

// Keep a- and c-variables only
keep if inlist(substr(widcode, 1, 1), "a", "c")

// Extend the currency to c- variables
rename currency currency2
egen currency = mode(currency2), by(iso year)
drop currency2

reshape wide value, i(iso year p) j(widcode) string

// Rescale composition variables so that they sum to 100%
foreach c in i t { 
	// First, look at capital vs labor income
	generate double totinc = valuecfilin992`c' + valuecficap992`c'
	// Sanity check: no major discrepancies
	assert abs(totinc - 1) < 2e-2 if (totinc < .)
	// Rescale all income accordingly
	foreach v of varlist valuecf* {
		replace `v' = `v'/totinc if (totinc < .)
	}
	drop totinc

	// Second, deal with labor income only
	generate double labinc = valuecfimil992`c' + valuecfiwag992`c'
	// Sanity check: no major discrepancies
	assert abs(labinc - valuecfilin992`c')/labinc < 1e-5 ///
		if (labinc < .) & (valuecfilin992`c' < .)
	// Adjust subcomponents of labor income
	foreach v of varlist valuecfimil992`c' valuecfiwag992`c' {
		replace `v' = `v'/labinc*valuecfilin992`c' if (labinc < .) & (valuecfilin992`c' < .)
	}
	drop labinc

	// Third, capital income
	generate double capinc = valuecfidiv992`c' + valuecfiint992`c' + valuecfikgi992`c' ///
		+ valuecfimik992`c' + valuecfiren992`c'
	// Sanity check: no major discrepancies
	assert abs(capinc - valuecficap992`c')/capinc < 1e-5 ///
		if (capinc < .) & (valuecficap992`c' < .)
	// Adjust subcomponents of capital income
	foreach v of varlist valuecfidiv992`c' valuecfiint992`c' valuecfikgi992`c' ///
			valuecfimik992`c' valuecfiren992`c' {
		replace `v' = `v'/capinc*valuecficap992`c' if (capinc < .) & (valuecficap992`c' < .)
	}
	drop capinc

	// Sanity check: everything sums up correctly
	generate double totinc = valuecfilin992`c' + valuecficap992`c'
	generate double labinc = valuecfimil992`c' + valuecfiwag992`c'
	generate double capinc = valuecfidiv992`c' + valuecfiint992`c' + valuecfikgi992`c' ///
		+ valuecfimik992`c' + valuecfiren992`c'
	// Still a very small discrepancy (< 1e-14) that Stata can't get rid of
	assert abs(totinc - 1) < 1e-14 if (totinc < .)
	assert abs(labinc + capinc - 1) < 1e-14 if (labinc < .) & (capinc < .)

	drop totinc labinc capinc
}

foreach v of varlist valuecf* {
	local c = substr("`v'", 15, 1)
	di "`c'"
	local av = "valuea" + substr("`v'", 7, .)
	replace `v' = `v'*valueafiinc992`c'
	rename `v' `av'
}

reshape long value, i(iso year p) j(widcode) string
	
drop if value >= .

tempfile newdb
save "`newdb'"

use "$work_data/add-macro-data-output.dta", clear
drop if substr(widcode, 1, 1) == "c"

generate new = 0
append using "`newdb'"
replace new = 1 if (new >= .)

duplicates tag iso p year widcode, generate(duplicate)
drop if (duplicate == 1) & (new == 0)
drop duplicate new

sort iso p widcode year
label data "Generated by calculate-income-categories.do"
save "$work_data/calculate-income-categories-output.dta", replace


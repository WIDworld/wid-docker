tempfile gfd_data

foreach iso in DE GH GR HK IT KE MU MW NG SC SG TW TZ UG ZM ZW {
	import delimited "$gfd_data/price-index/`iso'.csv", clear rowrange(4) varnames(3)
	destring close, force replace ignore(",")
	drop if (close >= .)
	generate year = real(substr(date, 7, 10))
	generate iso = "`iso'"
	rename close cpi_gfd
	keep iso year cpi_gfd
	keep if (year <= 2015)
	if ("`iso'" != "DE") {
		append using "`gfd_data'"
	}
	save "`gfd_data'", replace
}

label data "Generated by import-gfd-cpi.do"
save "$work_data/gfd-cpi.dta", replace

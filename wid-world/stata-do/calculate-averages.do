use "$work_data/convert-to-nominal-output.dta", clear

// Keep a- and s-variables only
keep if inlist(substr(widcode, 1, 1), "a", "s")

keep iso year p widcode value

generate inctype = substr(widcode, 2, .)
generate avg = value if (p == "pall")
egen avg2 = mode(avg), by(iso year inctype)
drop avg
rename avg2 avg
drop if p == "pall"
drop if substr(widcode, 1, 1) == "a"

generate p1 = real(regexs(1)) if regexm(p, "^p([0-9\.]+)p([0-9\.]+)$")
generate p2 = real(regexs(2)) if regexm(p, "^p([0-9\.]+)p([0-9\.]+)$")
replace p1 = real(regexs(1)) if regexm(p, "^p([0-9\.]+)$")
replace p2 = 100 if regexm(p, "^p([0-9\.]+)$")

replace p1 = round(p1/100, 0.00001)
replace p2 = round(p2/100, 0.00001)

replace value = value*avg/(p2 - p1)
replace p = "p" + string(100*p1) + "p" + string(100*p2)
replace widcode = "a" + inctype

keep iso p year widcode value

tempfile average
save "`average'"

use "$work_data/convert-to-nominal-output.dta", clear
append using "`average'"

label data "Generated by calculate-averages.do"
save "$work_data/calculate-averages-output.dta", replace
